\import Cat
\import Util
\import Paths
\import Category
\open Precat

\func \infix 4 ~= {C : Precat} (a b : C) => Iso {C} {a} {b}

\lemma compose-iso {C : Precat} {a b c : C} {f : Hom a b} {g : Hom b c} (fi : Iso f) (gi : Iso g) : Iso (f >> g) \cowith
  | inv => gi.inv >> fi.inv
  | f_inv =>
    \let | f' => fi.inv | g' => gi.inv
         | e1 =>
           g >> (g' >> f') ==< inv (o-assoc _ _ _ ) >==
           (g >> g') >> f' ==< pmap (o f') gi.f_inv >==
           id _ >> f'      ==< id-left _            >==
           f'              `qed
    \in
      f >> g >> (g' >> f')   ==< o-assoc _ _ _  >==
      f >> (g >> (g' >> f')) ==< pmap (>> f) e1 >==
      f >> f'                ==< fi.f_inv       >==
      id a                   `qed
  | inv_f =>
    \let | f' => fi.inv | g' => gi.inv
         | e1  : f' >> (f >> g) = g =>
           f' >> (f >> g) ==< inv (o-assoc _ _ _) >==
           (f' >> f) >> g ==< pmap (o g) fi.inv_f >==
           id _ >> g      ==< id-left _           >==
           g              `qed
    \in
      g' >> f' >> (f >> g)   ==< o-assoc _ _ _   >==
      g' >> (f' >> (f >> g)) ==< pmap (>> g') e1 >==
      g' >> g                ==< gi.inv_f        >==
      id c                   `qed

\lemma invert-iso {C : Precat} {a b : C} {f : Hom a b} (fi : Iso f) : Iso (fi.inv) \cowith
  | inv => f
  | f_inv => fi.inv_f
  | inv_f => fi.f_inv