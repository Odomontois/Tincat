\import Cat
\import Category
\import Iso
\import Unique
\import Paths

\record Terminal (C : Precat){
  | term : C
  \property terminal (x : C) : Singleton (x ->> term)
} \where {
  \func \infixl 5 term->> {C : Precat} (x : C)  (t : Terminal C) : x ->> term => elem {t.terminal x}

  \lemma iso {C : Precat} (t t' : Terminal C) : Iso (t'.term term->> t) \cowith
    | inv => elem {t'.terminal term}
    | inv_f => singleton {t'.terminal t'.term} _ _
    | f_inv => singleton {terminal term} _ _

  \lemma terminal->single {C : Cat} (t : Terminal C) : Singleton (Terminal C)  =>
    Singleton.prop->Singleton t

  \use \level univ-term-is-prop {C : Cat} (t t' : Terminal C) : t = t' =>
    \let  | t=t' : t.term = t'.term => Cat.iso-to-= (iso t' t)
          | p= =>  pathInProp (\lam i => \Pi (x : C) -> Singleton (x ->> (t=t' @ i))) t.terminal t'.terminal
    \in path (\lam i => \new Terminal C{
      | term => t=t' @ i
      | terminal => p= @ i
    })
}

\func UTerminal (C : Cat) : \Prop => Terminal C

\func Initial (C : Precat) => Terminal (opP C)

