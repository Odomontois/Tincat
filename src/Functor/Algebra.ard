\import Cat
\import Category
\import Functor
\import HLevel
\import Iso
\import Paths
\import Unique
\import Universal.Terminal
\open Precat
\open Algebra (Homo, =homo)

\record Algebra {C : Precat}(F : Functor C C) {
  | base : C
  | algebra : F.ob base ->> base
} \where {
  \record Homo {C : Precat} {F : Functor C C} (x y : Algebra F){
    | m : x.base ->> y.base
    \property homo : m `o` x.algebra = y.algebra `o` F.map m
  }

  \lemma =homo {C : Precat} {F : Functor C C } {a b : Algebra F} {u v : Homo a b} (p : u.m = v.m) : u = v =>
    path(\lam i => \new Homo a b {
      | m => p @ i
      | homo => pathInProp (\lam i => (p @ i) `o` a.algebra = b.algebra `o` F.map (p @ i)) u.homo v.homo @ i
    })
}

\instance  algebraCat {C : Precat} { F : Functor C C} : Precat (Algebra F) \cowith
  | Hom a b => Homo a b
  | id a => \new Homo a {
    | m => id _
    | homo => id-left *> inv id-right *> pmap (o (algebra {a})) (inv (F.functor_id _))
  }
  | o {_} {_} {z : Algebra} (f : Homo) (g : Homo) => \new Homo _ {
    | m => f.m `o` g.m
    | homo =>
      o-assoc *> pmap (o f.m) g.homo *> inv o-assoc *> (pmap (>> (F.map g.m)) f.homo *>
      o-assoc *> pmap (o z.algebra) (F.func_comp _ _))
  }
  | id-left  => =homo id-left
  | id-right => =homo id-right
  | o-assoc  => =homo o-assoc

-- | By Lambek
\lemma initial-fixpoint {C : Precat} {F : Functor C C} (initMu : Initial (algebraCat {C} {F})) : Iso (algebra  {initMu.term}) =>
  \let | mu : Algebra F => initMu.term
       | mualg : F.ob mu.base ->> mu.base => mu.algebra
       | mapped : Algebra F => \new Algebra F _ (F.map mu.algebra)
       | revhomo : Homo mu mapped => elem {initMu.terminal mapped}
       | rev : mu.base ->> F.ob (mu.base)  => revhomo.m
       | rev*mu=F_mu*rev : rev `o` mualg = F.map (mualg `o` rev)  => revhomo.homo *> F.func_comp _ _
       | mu*rev-homo : Homo mu mu => \new Homo mu mu{
         | m => mualg `o` rev
         | homo => o-assoc *> pmap (o mualg) rev*mu=F_mu*rev
       }
       | idhomo : Homo mu mu => \new Homo mu mu {
         | m => id _
         | homo => id-left *> inv id-right *> pmap (o mualg) (inv (F.functor_id _))
       }
       | mumu-single : isProp (Homo mu mu) => singleton {initMu.terminal mu}
       | mu*rev=id  : mualg `o` rev = id _ => pmap (\lam (h : Homo mu mu) => h.m) (mumu-single mu*rev-homo idhomo)
       | rev*mu=id : rev `o` mualg = id _  => rev*mu=F_mu*rev *> pmap F.map mu*rev=id *> F.functor_id _

  \in \new Iso mualg rev {
    | inv_f => rev*mu=id
    | f_inv => mu*rev=id
  }
